&НаКлиенте
Процедура ЗагрузитьФайл()
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.МножественныйВыбор = Ложь;
	ПараметрыДиалога.Заголовок = НСтр("ru = 'Выберите файл'");
	
    ЗавершениеОбратныйВызов = Новый ОписаниеОповещения("ЗавершениеОбратныйВызов", ЭтотОбъект);
    ПрогрессОбратныйВызов = Новый ОписаниеОповещения("ПрогрессОбратныйВызов", ЭтотОбъект);
	
    НачатьПомещениеФайлаНаСервер(
		ЗавершениеОбратныйВызов,
		ПрогрессОбратныйВызов,
		,,
		ПараметрыДиалога,
		ЭтаФорма.УникальныйИдентификатор
	);
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеОбратныйВызов(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт	
	Если ОписаниеПомещенногоФайла = Неопределено Или ОписаниеПомещенногоФайла.ПомещениеФайлаОтменено Тогда
		Возврат;
	КонецЕсли;

	Объект.Наименование = ОписаниеПомещенногоФайла.СсылкаНаФайл.Имя;
	СсылкаНаДанныеФайла = ОписаниеПомещенногоФайла.Адрес;
	ЭтоКартинка = РаботаСФайламиКлиентСервер.ЭтоКартинка(Объект.Наименование);
	
	Если Не ЭтоКартинка Тогда
		Элементы.Предпросмотр.Видимость = Ложь;
	Иначе
		Элементы.Предпросмотр.Видимость = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПрогрессОбратныйВызов(ПомещаемыйФайл, Помещено, ОтказОтПомещенияФайла, ДополнительныеПараметры) Экспорт
    Состояние("Файл " + ПомещаемыйФайл.Имя, Помещено, "Файл имеет размер " + ПомещаемыйФайл.Размер() + " байт", БиблиотекаКартинок.Документ);
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ЭтоАдресВременногоХранилища(СсылкаНаДанныеФайла) Тогда
		ТекущийОбъект.ФайлХранилище = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(СсылкаНаДанныеФайла));
		ТекущийОбъект.Изменен = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЭтоКартинка = РаботаСФайламиКлиентСервер.ЭтоКартинка(Объект.Наименование);

	Если ЭтоКартинка Тогда
		СсылкаНаДанныеФайла = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "ФайлХранилище");
	Иначе
		Элементы.Предпросмотр.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры
